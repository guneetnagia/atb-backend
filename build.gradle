buildscript {
    ext['spring-framework.version'] = '6.1.6'
    ext['netty.version'] = '4.1.109.Final'
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.barfuin.gradle.jacocolog' version '3.1.0'
}

group = 'guneet.atb'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Reactive Mongo
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    // Reactive R2DBC MySQL
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework:spring-r2dbc:6.0.11'
    implementation 'io.asyncer:r2dbc-mysql'
    implementation 'org.json:json:20231013'
    // s3
    implementation platform('software.amazon.awssdk:bom:2.20.56')
    implementation 'software.amazon.awssdk:s3:'
    implementation group: 'software.amazon.awssdk', name: 'sts', version: '2.21.1'
    implementation 'software.amazon.awssdk:netty-nio-client:2.21.2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Airbrake
    implementation 'io.airbrake:javabrake:0.3.0'

    // Swagger
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:2.2.0"

    //Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.h2database:h2:2.2.224'
    testImplementation 'io.r2dbc:r2dbc-h2'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.12.0'
    testImplementation 'org.meanbean:meanbean:2.0.3'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
    testImplementation 'com.redis.testcontainers:testcontainers-redis-junit-jupiter:1.4.6'
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.destination file("${buildDir}/jacocoHtml")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/guneet/atb/reactive/ATBServiceApplication*",
                    "com/guneet/atb/reactive/config/LogAspect*"
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.05
            }
        }
    }
}

test {
    systemProperty 'spring.profiles.active', 'test'
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}